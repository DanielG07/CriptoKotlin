apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'org.jetbrains.dokka'

apply from: '../ktlint.gradle'
apply from: '../dependencies.gradle'

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.danielg7.cripto"
        minSdk 28
        targetSdk 32
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }
    }
    signingConfigs {
        def keystoreDegubPropertiesFile = rootProject.file("keystore.debug.properties")
        def keystoreDebugProperties = new Properties()
        keystoreDebugProperties.load(new FileInputStream(keystoreDegubPropertiesFile))

        def keystorePropertiesFile = rootProject.file("keystore.properties")
        def keystoreProperties = new Properties()
        keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

        debug {
            storeFile file(keystoreDebugProperties['appStoreFile'])
            storePassword keystoreDebugProperties['appStorePassword']
            keyAlias keystoreDebugProperties['appKeyAlias']
            keyPassword keystoreDebugProperties['appKeyPassword']
        }
        release {
            storeFile file(keystoreProperties['appStoreFile'])
            storePassword keystoreProperties['appStorePassword']
            keyAlias keystoreProperties['appKeyAlias']
            keyPassword keystoreProperties['appKeyPassword']
        }
    }
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            resValue "string", "app_name", "Criptos (dev)"
            signingConfig signingConfigs.debug
            minifyEnabled false
            shrinkResources false
            debuggable true
            buildConfigField("String", "BITSO_API_URL", "\"https://api.bitso.com\"")
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled true
            shrinkResources true
            debuggable false
            buildConfigField("String", "BITSO_API_URL", "\"https://api.bitso.com\"")
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

    }
    flavorDimensions "default"
    productFlavors {
        mexico {
            resValue "string", "app_name", "Criptos (MX)"
            applicationId "com.danielg7.cripto.mx"
            buildConfigField("String", "PHONE_NUMBER", "\"5528490744\"")
        }
        brazil {
            resValue "string", "app_name", "Criptos (BR)"
            applicationId "com.danielg7.cripto.br"
            buildConfigField("String", "PHONE_NUMBER", "\"5528490744\"")
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

}

dependencies {

    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.activity:activity-compose:1.3.1'
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    //Network
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-gson:2.9.0"
    implementation "com.squareup.okhttp3:okhttp:4.9.1"
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2"

    //Hilt
    implementation "com.google.dagger:hilt-android:2.38.1"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"
    kapt "com.google.dagger:hilt-android-compiler:2.38.1"

    //Firebase
    implementation platform('com.google.firebase:firebase-bom:30.2.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    //Debug
    implementation 'com.jakewharton.timber:timber:5.0.1'


    //Persistence
    implementation "androidx.room:room-runtime:2.5.0-alpha02"
    implementation "androidx.room:room-ktx:2.5.0-alpha02"
    implementation "androidx.room:room-guava:2.5.0-alpha02"
    kapt "androidx.room:room-compiler:2.5.0-alpha02"
    kapt "android.arch.persistence.room:compiler:1.1.1"
    testImplementation "androidx.room:room-testing:2.5.0-alpha02"




    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"
}

check.dependsOn ktlint

tasks.dokkaHtml.configure {
    outputDirectory.set(file("../docs"))
}